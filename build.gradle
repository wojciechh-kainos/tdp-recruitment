group 'tdp-recruitment'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eu.appsatori.fatjar'

mainClassName = "TdpRecruitmentApplication"

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
    }
    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}

project.ext {
    dropwizardVersion = '0.9.3'
    configPath = './src/main/resources/app_config.yml'
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly
}

sourceSets {

    main {
        compileClasspath = configurations.compile + configurations.compileOnly
    }

    test {
        compileClasspath = compileClasspath + configurations.compileOnly
    }

    selenium {
        compileClasspath = compileClasspath + configurations.compileOnly
    }
}


dependencies {
    compile (
            'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-assets:' + dropwizardVersion,
            'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
            'io.dropwizard:dropwizard-auth:' + dropwizardVersion,
            'io.dropwizard:dropwizard-db:' + dropwizardVersion,
//            'io.dropwizard:dropwizard-forms:' + dropwizardVersion,
            'io.dropwizard:dropwizard-migrations:' + dropwizardVersion,
            'com.github.dirkraft.dropwizard-file-assets:dropwizard-file-assets:0.0.2'
    )

    compile 'org.postgresql:postgresql:9.4.1208.jre7'

    /*Simple Java Mail*/
    compile 'org.simplejavamail:simple-java-mail:4.1.1'

    /* Google Guice - Dependency Injection */
    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'

    /* Dropwizard-Guice */
    compile group: 'com.hubspot.dropwizard', name: 'dropwizard-guice', version: '0.8.4.0'

    testCompile "org.mockito:mockito-core:1.+"
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'com.googlecode.jmockit:jmockit:1.2'
    testCompile 'com.h2database:h2:1.3.172'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// Configure the fatJar task
fatJar {
    classifier 'fat'

    // We need to add the main class to the manifest, otherwise the JAR won't start.
    manifest {
        attributes 'Main-Class': mainClassName
    }

    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

task migrate(type: JavaExec, dependsOn: fatJar) {
    main = "-jar"
    args = ["./build/libs/" + rootProject.name + "-" + version + "-fat.jar", "db", "migrate", configPath]
}


run {
    args 'server', configPath
}
